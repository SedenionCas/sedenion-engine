number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

unary_minus =  { "-" }
primary     = _{ number | "(" ~ expr ~ ")" }
atom        = _{ constant | function | unary_minus? ~ (monomial | primary) }

function_name = @{ ASCII_ALPHA+ }
function_args =  { expr ~ ("," ~ expr)* }
function      =  { function_name ~ "(" ~ function_args ~ ")" }

variable    = @{ ASCII_ALPHA_LOWER }
coefficient =  { number }
exponent    =  { power ~ number }
monomial    =  { coefficient? ~ variable ~ exponent? }

constant = @{ ASCII_ALPHA_UPPER+ }

bin_op   = _{ add | subtract | multiply | divide | modulo | power | equals }
add      =  { "+" }
subtract =  { "-" }
multiply =  { "*" }
divide   =  { "/" }
modulo   =  { "%" }
power    =  { "^" }
equals   =  { "=" }

expr = { atom ~ (bin_op ~ atom)* }

equation = _{ SOI ~ expr ~ EOI }

WHITESPACE = _{ " " }
